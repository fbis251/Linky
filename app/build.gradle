apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId 'com.fernandobarillas.linkshare'
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 15
        versionName '2.7 API/0.7'
    }
    dataBinding {
        enabled = true
    }
    signingConfigs {
        release {
            readKeystoreProperties()
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField 'boolean', 'USE_HTTP_PROXY', USE_HTTP_PROXY
            it.buildConfigField 'String', 'HTTP_PROXY_ADDRESS', HTTP_PROXY_ADDRESS
            it.buildConfigField 'int', 'HTTP_PROXY_PORT', HTTP_PROXY_PORT
        }
        release {
            buildConfigField 'boolean', 'USE_HTTP_PROXY', 'false'
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
        }
    }
    splits {
        abi {
            enable project.hasProperty('abiSplitBuild')
            reset()
            include 'armeabi-v7a', 'mips', 'x86'
            universalApk true
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.github.Kennyc1012:BottomSheet:2.3.1'
    compile 'com.github.varunest:sparkbutton:1.0.4'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.takisoft.fix:preference-v7:25.2.0.0'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.1'
    testCompile 'junit:junit:4.12'
}

def readKeystoreProperties() {
    def propsPath = '/tmp/keystore/fb_rsa_4096.keystore.properties'
    def propsFile = rootProject.file(propsPath)
    def configName = 'release'
    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        android.signingConfigs[configName].storeFile = file(props['storeFile'])
        android.signingConfigs[configName].storePassword = props['storePassword']
        android.signingConfigs[configName].keyAlias = props['keyAlias']
        android.signingConfigs[configName].keyPassword = props['keyPassword']
        println '===== read keystore properties success ====='
    }
}
