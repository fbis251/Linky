apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    defaultConfig {
        applicationId "com.fernandobarillas.linkshare"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 9
        versionName "2.3 API/0.6"
    }
    dataBinding {
        enabled = true
    }
    signingConfigs {
        release {
            readKeystoreProperties()
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
    }
    buildTypes {
        buildTypes.each {
            it.buildConfigField 'boolean', 'USE_HTTP_PROXY', USE_HTTP_PROXY
            it.buildConfigField 'String', 'HTTP_PROXY_ADDRESS', HTTP_PROXY_ADDRESS
            it.buildConfigField 'int', 'HTTP_PROXY_PORT', HTTP_PROXY_PORT
        }
        release {
            buildConfigField 'boolean', 'USE_HTTP_PROXY', 'false'
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-realm.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha7'
    compile 'com.github.varunest:sparkbutton:1.0.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    testCompile 'junit:junit:4.12'
}

def readKeystoreProperties() {
    def propsPath = '/tmp/keystore/fb_rsa_4096.keystore.properties'
    def propsFile = rootProject.file(propsPath)
    def configName = 'release'
    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        android.signingConfigs[configName].storeFile = file(props['storeFile'])
        android.signingConfigs[configName].storePassword = props['storePassword']
        android.signingConfigs[configName].keyAlias = props['keyAlias']
        android.signingConfigs[configName].keyPassword = props['keyPassword']
        println "===== read keystore properties success ====="
    }
}
